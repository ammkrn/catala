## Section 1001

```catala-metadata
declaration enumeration GainOrLoss:
  -- Gain content money
  -- Loss content money
  
# This could be improved by an `option A` type; the s164 property tax stuff and
# the additional information about the adjusted basis and any term interest would 
# be two optional structs.
declaration scope Section1001:
  input fmv_property_received content money
  input money_received content money
  input adjusted_basis content money
  # Nothing is done with this arg, but making it separate should signal to
  # users that it's NOT supposed to be considered part of the other S164 costs.
  input reimbursed_s164_property_tax content money
  input unreimbursed_s164_property_tax_paid_by_purchaser content money
  # The sum of these `amount_adjusted_basis_attributable_to_*` values must be
  # less than or equal to `adjusted_basis`. 
  input amount_adjusted_basis_attributable_to_1014 content money
  input amount_adjusted_basis_attributable_to_1015 content money
  input amount_adjusted_basis_attributable_to_1041 content money
  input entire_term_interest_transferred content boolean

  # The adjusted basis minus the portions attributable to 1014, 1015, or 1041 if the
  # property being transferred is a qualifying term interest, and the taxpayer is not 
  # transferring their entire interest in the property.
  internal effective_adjusted_basis content money
  internal amount_realized content money
  internal gain_or_loss_realized content GainOrLoss

  output gain_or_loss_recognized content GainOrLoss
```

### (a) Computation of gain or loss

The gain from the sale or other disposition of property shall be the excess of
the amount realized therefrom over the adjusted basis provided in section 1011
for determining gain, and the loss shall be the excess of the adjusted basis
provided in such section for determining loss over the amount realized.

```catala
scope Section1001:
  definition gain_or_loss_realized under condition
    amount_realized >=$ effective_adjusted_basis
  consequence equals
    Gain content (amount_realized -$ effective_adjusted_basis)

  definition gain_or_loss_realized under condition
    effective_adjusted_basis >$ amount_realized
  consequence equals
    Loss content (effective_adjusted_basis -$ amount_realized)
```

### (b) Amount realized

The amount realized from the sale or other disposition of property shall be the
sum of any money received plus the fair market value of the property (other than
money) received. In determining the amount realized—

```catala
scope Section1001:
  definition amount_realized equals 
    fmv_property_received +$ money_received +$ unreimbursed_s164_property_tax_paid_by_purchaser
```

#### (1) 
there shall not be taken into account any amount received as reimbursement
for real property taxes which are treated under section 164(d) as imposed on the
purchaser, and

#### (2)
there shall be taken into account amounts representing real property taxes
which are treated under section 164(d) as imposed on the taxpayer if such taxes
are to be paid by the purchaser.


### (c) Recognition of gain or loss
Except as otherwise provided in this subtitle, the entire amount of the gain or
loss, determined under this section, on the sale or exchange of property shall
be recognized.

```catala
scope Section1001:
  definition gain_or_loss_recognized equals gain_or_loss_realized

# SL: this is weird, not sure what this is supposed to indicate computationally
# speaking
```

### (d) Installment sales

Nothing in this section shall be construed to prevent (in the case of property
sold under contract providing for payment in installments) the taxation of that
portion of any installment payment representing gain or profit in the year in
which such payment is received.

### (e) Certain term interests

#### (1) In general

In determining gain or loss from the sale or other disposition of a term
interest in property, that portion of the adjusted basis of such interest
which is determined pursuant to section 1014, 1015, or 1041 (to the extent
that such adjusted basis is a portion of the entire adjusted basis of the
property) shall be disregarded.


```catala
scope Section1001:
  definition effective_adjusted_basis under condition 
    not entire_term_interest_transferred 
    consequence equals
      adjusted_basis -$ 
      (amount_adjusted_basis_attributable_to_1014 
      +$ amount_adjusted_basis_attributable_to_1015 
      +$ amount_adjusted_basis_attributable_to_1041)
```

#### (2) Term interest in property defined

For purposes of paragraph (1), the term “term interest in property” means—

(A) a life interest in property,

(B) an interest in property for a term of years, or

(C) an income interest in a trust.

```catala
# CB: There should be a way to get the rendered output (pdf/tex) to show readers the 
# code that accounts for this, even if it's up in the metadata.
```

#### (3) Exception

Paragraph (1) shall not apply to a sale or other disposition which is a part of
a transaction in which the entire interest in property is transferred to
any person or persons.

```catala
scope Section1001:
  definition effective_adjusted_basis under condition
    entire_term_interest_transferred
  consequence equals adjusted_basis
```
