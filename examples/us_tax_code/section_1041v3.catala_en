> Include: ./preamble.catala_en

This one doesn't split up subsection a, but does guard subsection (e), which are
the trust parts. 

Interpreting Test3 returns the error below, because not all states have been
reached. subsection (e), which would otherwise step through `with_trust` 
does not apply if subsection (a) does not apply. :

[ERROR] This variable evaluated to an empty term (no rule that defined it applied in this situation)

  --> ./examples/us_tax_code/tests/../section_1041v3.catala_en
   |
39 |     state with_trust
   |           ^^^^^^^^^^
   + 26 U.S. Code § 1041 - Transfers of property between spouses or incident to divorce

## 26 U.S. Code § 1041 - Transfers of property between spouses or incident to divorce

```catala-metadata
declaration enumeration MaritalStatus:
  -- Married
  -- DivorcedAsOf content date

# The transferee can either be a spouse (or former spouse), or a trust
# for the benefit of the spouse/former spouse.
declaration enumeration Transferee:
  -- Spouse
  # content is the sum of the liabilities assumed by the trust, and the
  # liabilites to which the property is subject. See `Test2` for an example.
  -- Trust content money


declaration scope Section1041:
  input transferor_adjusted_basis content money
  input transfer_date content date
  input transferee content Transferee
  input marital_status_as_of_transfer_date content MaritalStatus
  input transfer_related_to_cessation_of_marriage content boolean
  input spouse_is_nonresident_alien content boolean
  input fair_market_value content money

  internal transfer_incident_to_divorce condition
  internal subsection_a_applies condition

  output gain_or_loss_recognized content money
    state without_trust
    state with_trust

  output transferee_adjusted_basis content money
    state without_trust
    state with_trust

scope Section1041:
  # This is a bit awkward, if section a does not apply we need to return
  # something anyway, hence this definition
  label base 
  definition transferee_adjusted_basis state without_trust under condition
    not subsection_a_applies
  consequence equals transferor_adjusted_basis $+ gain_or_loss_recognized
```

### (a) General rule

No gain or loss shall be recognized on a transfer of property from an individual to (or in trust for the benefit of)—

(1) a spouse, or
(2) a former spouse, but only if the transfer is incident to the divorce.

```catala
scope Section1041:
  label base 
  rule subsection_a_applies under condition
    match marital_status_as_of_transfer_date with pattern
     -- Married: true
     -- DivorcedAsOf of date_divorce: transfer_incident_to_divorce
  consequence fulfilled

  definition gain_or_loss_recognized state without_trust equals $0
```

### (b) Transfer treated as gift; transferee has transferor’s basis

In the case of any transfer of property described in subsection (a)—
(1) for purposes of this subtitle, the property shall be treated as acquired by the transferee by gift, and
(2) the basis of the transferee in the property shall be the adjusted basis of the transferor.

```catala
scope Section1041 under condition subsection_a_applies:
  label base 
  definition transferee_adjusted_basis state without_trust equals transferor_adjusted_basis
```

### (c) Incident to divorce

For purposes of subsection (a)(2), a transfer of property is incident to the divorce if such transfer—

(1) occurs within 1 year after the date on which the marriage ceases, or
(2) is related to the cessation of the marriage.

```catala
scope Section1041:
  definition transfer_incident_to_divorce equals
    transfer_related_to_cessation_of_marriage
    or (match marital_status_as_of_transfer_date with pattern
      -- DivorcedAsOf of divorce_date: transfer_date -@ divorce_date <=^ 1 year
      -- Married: false)
```

### (d) Special rule where spouse is nonresident alien

Subsection (a) shall not apply if the spouse (or former spouse) of the individual
making the transfer is a nonresident alien.

```catala
scope Section1041:
  #label nonresident 
  exception base rule subsection_a_applies under condition
    spouse_is_nonresident_alien
  consequence not fulfilled
```

### (e) Transfers in trust where liability exceeds basis

Subsection (a) shall not apply to the transfer of property in trust to the extent that—

(1) the sum of the amount of the liabilities assumed, plus the amount of the liabilities
to which the property is subject, exceeds

(2) the total of the adjusted basis of the property transferred.


```catala
 scope Section1041 under condition subsection_a_applies:
   definition gain_or_loss_recognized state with_trust equals
     match transferee with pattern
     -- Spouse: gain_or_loss_recognized
     -- Trust of sum_liabilities: 
       (if (sum_liabilities -$ transferor_adjusted_basis) <$ $0 
       then $0 
       else (sum_liabilities -$ transferor_adjusted_basis))
```

Proper adjustment shall be made under subsection (b) in the basis of the transferee in
such property to take into account gain recognized by reason of the preceding sentence.

```catala
scope Section1041 under condition subsection_a_applies:
  definition transferee_adjusted_basis state with_trust equals
    if
      transferee with pattern Trust 
    then
      match transferee with pattern
      -- Spouse: $0 # should not happen
      -- Trust of sum_liabilities: transferee_adjusted_basis +$ gain_or_loss_recognized
    else
      (transferee_adjusted_basis +$ gain_or_loss_recognized)
```





